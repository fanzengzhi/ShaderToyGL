// The MIT License
// Copyright Â© 2013 Inigo Quilez
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


// learn more here: // http://www.iquilezles.org/www/articles/distancefractals/distancefractals.htm 

// from: https://www.shadertoy.com/view/Mss3R8

float calc( vec2 p, float time )
{
    p = -1.0 + 2.0*p;
    p.x *= iResolution.x/iResolution.y;

    float ltime = 0.5-0.5*cos(time*0.12);
    float zoom = pow( 0.9, 100.0*ltime );
    float an = 2.0*ltime;
    p = mat2(cos(an),sin(an),-sin(an),cos(an))*p;
    vec2 ce = vec2( 0.2655,0.301 );
    ce += zoom*0.8*cos(4.0+4.0*ltime);
    p = ce + (p-ce)*zoom;
    vec2 c = vec2( -0.745, 0.186 ) - 0.045*zoom*(1.0-ltime);
    
    vec2 z = p;
   
#if 0
    // full derivatives version
    vec2 dz = vec2( 1.0, 0.0 );
    for( int i=0; i<256; i++ )
    {
        dz = 2.0*vec2(z.x*dz.x-z.y*dz.y, z.x*dz.y + z.y*dz.x );
        z = vec2( z.x*z.x - z.y*z.y, 2.0*z.x*z.y ) + c;
        if( dot(z,z)>200.0 ) break;
    }
    float d = sqrt( dot(z,z)/dot(dz,dz) )*log(dot(z,z));

#else
    // only derivative length version
    float ld2 = 1.0;
    float lz2 = dot(z,z);
    for( int i=0; i<256; i++ )
    {
        z = vec2( z.x*z.x - z.y*z.y, 2.0*z.x*z.y ) + c;
        ld2 *= 4.0*lz2;
        lz2 = dot(z,z);
        if( lz2>200.0 ) break;
    }
    float d = sqrt(lz2/ld2)*log(lz2);

#endif
    
    return pow( clamp( (150.0/zoom)*d, 0.0, 1.0 ), 0.5 );
}

    
void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    #if 1
    float scol = calc( fragCoord.xy/iResolution.xy, iTime );
    #else

    float scol = 0.0;
    for( int j=0; j<2; j++ )
    for( int i=0; i<2; i++ )
    {
        vec2 of = -0.5 + vec2( float(i), float(j) ) / 2.0;
        scol += calc( (fragCoord.xy+of)/iResolution.xy, iTime );
    }
    scol *= 0.25;

    #endif
    
    vec3 vcol = pow( vec3(scol), vec3(0.9,1.1,1.4) );
    
    vec2 uv = fragCoord.xy/iResolution.xy;
    //vcol *= 0.7 + 0.3*pow(16.0*uv.x*uv.y*(1.0-uv.x)*(1.0-uv.y),0.25);

    
    fragColor = vec4( vcol, 1.0 );
}